plugins {
	id 'maven-publish'
	id 'com.github.hierynomus.license' version '0.16.1' apply false
	id 'fabric-loom' version '1.7-SNAPSHOT' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version 'ce1aeb2b'

	// https://github.com/Fallen-Breath/yamlang
	id 'me.fallenbreath.yamlang' version '1.4.0' apply false
}

preprocess {
	def mc114 = createNode('1.14.4', 1_14_04, '')
	def mc115 = createNode('1.15.2', 1_15_02, '')
	def mc116 = createNode('1.16.5', 1_16_05, '')
	def mc117 = createNode('1.17.1', 1_17_01, '')
	def mc118 = createNode('1.18.2', 1_18_02, '')
	def mc119 = createNode('1.19.4', 1_19_04, '')
	def mc120 = createNode('1.20.6', 1_20_06, '')
	def mc121 = createNode('1.21'  , 1_21_00, '')

	mc115.link(mc114, null)
	mc115.link(mc116, null)
	mc116.link(mc117, null)
	mc117.link(mc118, null)
	mc118.link(mc119, null)
	mc119.link(mc120, null)
	mc120.link(mc121, null)
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
