pluginManagement {
	repositories {
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			name = 'Jitpack'
			url = "https://jitpack.io"
		}
		maven { url "https://maven.architectury.dev/" }
		maven { url "https://files.minecraftforge.net/maven/" }
		mavenCentral()
		gradlePluginPortal()
	}
	resolutionStrategy {
		eachPlugin {
			switch (requested.id.id) {
				case "com.replaymod.preprocess": {
					useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
					break
				}
			}
		}
	}
}

def versions = Arrays.asList(
		"1.14.4-fabric",
		"1.15.2-fabric",
		"1.16.5-fabric",
		"1.17.1-fabric",
		"1.18.2-fabric",
		"1.19.4-fabric",
		"1.19.4-forge",
		"1.20-fabric",
)
for (String version : versions) {
	include(":$version")

	def proj = project(":$version")
	proj.projectDir = file("versions/$version")
	proj.buildFileName = "../../common.gradle"
}
